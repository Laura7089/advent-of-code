#!/bin/env -S just --justfile

TODAY := `date +%d`

# create and edit a new day
day day=TODAY: (newday day)
    $EDITOR "./src/day{{ day }}.rs"

[private]
newday day:
    [ -f "./src/day{{ day }}.rs" ] || jinja2 \
        -D "day={{ day }}" \
        ./day_template.rs.j2 \
        -o "./src/day{{ day }}.rs"
    # cursed
    grep 'mod day{{ day }};' ./src/lib.rs || \
        sed -i 's/aoc_lib/mod day{{ day }};\naoc_lib/' ./src/lib.rs

# generate a flamegraph profile for a day
flamegraph day=TODAY *args="--ignore-status":
    -cargo aoc -d{{day}} --profile
    flamegraph {{args}} -- \
        ./target/aoc/aoc-autobuild/target/release/aoc-autobuild
